! Copyright 2023 Exciting developers
!
! Licensed under the Apache License, Version 2.0 (the "License");
! you may not use this file except in compliance with the License.
! You may obtain a copy of the License at
!
!   http://www.apache.org/licenses/LICENSE-2.0
!
! Unless required by applicable law or agreed to in writing, software
! distributed under the License is distributed on an "AS IS" BASIS,
! WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
! implied. See the License for the specific language governing
! permissions and limitations under the License.

!> @file
!> This file contains types to handle world and objects as if they were generated by MAGMA
!> but using CPU

!> Module containing types to handle MAGMA like but for CPU so the code does not need to be rewritten
module m_cpu_magma_t
    
    use iso_c_binding
    use m_constants, only: i32, i64, r64

    implicit none

    private
    public  linalg_world_t, linalg_obj_t

    !> Type to handle the GPU Magma queue and world
    type linalg_world_t
        !> Device id taking care of control
        integer :: device = 0
        !> GPU queue
        type(C_ptr), private  :: queue    = C_null_ptr
    contains
        procedure, public :: init, finish, is_queue_set, get_queue
    end type linalg_world_t


    !> Type to handle matrices in the GPU, if GPU is not enanbled it handles normal matrices
    type linalg_obj_t
        !> The pointer of the object (matrix/vector) in the CPU
        class(*), pointer, private  :: dA => null()
        !> Rows
        integer, private :: n_rows    = -1
        !> Cols
        integer, private :: n_cols    = -1
        !> Element size
        integer, private :: kind_size = -1
        !> Kind of the obj
        character(len=:), allocatable, private :: kind
        !> Rank of the element
        integer, private :: rank = -1
    contains
        procedure, public :: allocate_gpu, transfer_cpu_gpu, transfer_gpu_cpu
        procedure, public :: gpu_ptr, rows, cols, get_kind
        procedure, public :: destroy 
    end type linalg_obj_t

contains

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    !                   CPU WORLD                         !
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> This subroutine inits the dummy object
    !> @param[in] this - the GPU magma world to initialize
    !> @param[in] device - (optional) the device that should hold the queue (default: 0, which is the CPU)
    subroutine init(this, device)
        class(linalg_world_t), intent(inout) :: this
        integer, optional,  intent(in)    :: device
    end subroutine init

    !> This subroutine finishes the dummy object
    !> @param[in] this - the queue to finish
    subroutine finish(this)
        class(linalg_world_t), intent(inout) :: this
    end subroutine finish

    !> This function returns true 
    !> @param[in] this - the world to check if has inited queue
    pure function is_queue_set(this) result(answer)
        class(linalg_world_t), intent(in) :: this
        logical :: answer
        answer = .true.
    end function is_queue_set

    !> This is a getter for the queue, it returns the null pointer
    !> @param[in] this - the world object from which the queue is retrieved
    function get_queue(this) result(queue)

        class(linalg_world_t), target, intent(in) :: this
        type(C_ptr), pointer :: queue
        queue => this%queue

    end function get_queue

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    !                   UTILS                             !
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> This polymorphic function returns the size of this kind in bytes (as Intel does not have it)
    !> @param[in] this - the element from which the sizeof is asked
    !> @result kind_size - the size of the kind of this in bytes
    pure function sizeof_this(this) result(kind_size)
                
        class(*), intent(in) :: this(..)
        integer(i32) :: kind_size

        kind_size = STORAGE_SIZE(this) / STORAGE_SIZE('1')

    end function sizeof_this

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    !                   GPU OBJECTS                       !
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> This deallocates the object in the GPU 
    !> @param[in] this - gpu_matrix object to destroy
    subroutine destroy(this, queue)
        
        class(linalg_obj_t), intent(inout) :: this
        type(C_ptr), intent(inout)      :: queue

        if (associated(this%dA)) nullify(this%dA)
        
        this%n_rows    = -1
        this%n_cols    = -1
        this%kind_size = -1
        this%rank      = -1

    end subroutine destroy

    !> This subroutine allocates the matrix in the GPU
    !> @param[in] this - gpu_matrix object for which GPU memory is allocated
    !> @param[in] A    - object to mold the allocation
    subroutine allocate_gpu(this, A)
        
        class(linalg_obj_t), intent(inout)  :: this
        class(*), target, contiguous, intent(in) :: A(..)

        integer :: info
        integer(C_size_t) :: nbytes

        ! Select the rank of the object
        select rank(A)
            rank(1)
                this%rank   = rank(A)
                this%n_rows = size(A,1)
                this%n_cols = 1 
                this%dA => A(1)
                select type(A)
                    type is (real(r64))
                        this%kind = 'r64'
                    type is (complex(r64))
                        this%kind = 'c64'
                    class default
                        error stop "linalg_obj_t%allocate: Error in kind, only real and complex (r64) are accepted"
                end select

            rank(2)
                this%rank   = rank(A)
                this%n_rows = size(A,1)
                this%n_cols = size(A,2)
                this%dA => A(1,1)
                select type(A)
                    type is (real(r64))
                        this%kind = 'r64'
                    type is (complex(r64))
                        this%kind = 'c64'
                    class default
                        error stop "linalg_obj_t%allocate: Error in kind, only real and complex (r64) are accepted"
                end select

            rank default
                error stop "linalg_obj_t%allocate: Error in rank, only 1 and 2 are accepted"
        end select

        ! Allocating memory in the GPU
        this%kind_size = sizeof_this(A)

        nbytes = int(this%n_cols * this%n_rows * this%kind_size, C_size_t)

        if (info /= 0) error stop "linalg_obj_t%allocate: Error in allocating CPU memory"

    end subroutine allocate_gpu

    !> This subroutine fills a GPU matrix (sync) [DUMMY]
    !> @param[in] this - gpu_matrix object for which GPU memory is allocated
    !> @param[in] A    - object to transfer to the GPU
    !> @param[in] queue  - queue for GPU operations 
    subroutine transfer_cpu_gpu(this, A, queue)
        class(linalg_obj_t), intent(inout)          :: this
        class(*), contiguous, intent(inout), target :: A(..)
        type(C_ptr), intent(inout)                  :: queue
    end subroutine transfer_cpu_gpu

    !> This subroutine retrieve a GPU element to the CPU (sync) [DUMMY]
    !> @param[in] this - gpu_matrix object for which GPU memory is allocated
    !> @param[in] A    - object to which the element from the GPU would be transfered to
    !> @param[in] queue  - queue for GPU operations 
    subroutine transfer_gpu_cpu(this, A, queue)
        class(linalg_obj_t), intent(inout)          :: this
        class(*), contiguous, intent(inout), target :: A(..)
        type(C_ptr), intent(inout)                  :: queue
    end subroutine transfer_gpu_cpu

    !> This function retrieves the pointer to the GPU data
    !> @param[in] this - the obj from which retrieve the pointer to GPU data
    function gpu_ptr(this) result(answer)
        class(linalg_obj_t), intent(in) :: this
        class(*), pointer :: answer
        answer => this%dA 
    end function gpu_ptr

    !> This function retrieves the number of rows
    !> @param[in] this - the obj from which retrieve info
    pure integer function rows(this)       
        class(linalg_obj_t), intent(in) :: this
        rows = this%n_rows
    end function rows

    !> This function retrieves the number of cols
    !> @param[in] this - the obj from which retrieve info
    pure integer function cols(this)       
        class(linalg_obj_t), intent(in) :: this
        cols = this%n_cols
    end function cols

    !> This function retrieves the number of cols
    !> @param[in] this - the obj from which retrieve info
    pure function get_kind(this) result(answer)
        class(linalg_obj_t), intent(in) :: this
        character(len=:), allocatable :: answer
        answer = this%kind
    end function get_kind

end module m_cpu_magma_t
